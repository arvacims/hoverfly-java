plugins {
    id 'net.researchgate.release' version '2.4.0'
    id "de.undercouch.download" version "3.1.2"
    id "com.github.hierynomus.license" version "0.14.0"
    id 'com.github.ben-manes.versions' version '0.14.0'
    id 'java'
    id 'idea'
    id 'jacoco'
}

group = 'com.github.arvacims'
archivesBaseName = 'hoverfly-java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext."release.useAutomaticVersion" = true

repositories {
    mavenCentral()
}

dependencies {
    compile 'junit:junit:4.12'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.8'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.8'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.8.8'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.zeroturnaround:zt-exec:1.9'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    testCompile 'com.sun.jersey:jersey-client:1.19.3'
    testCompile 'com.google.guava:guava:20.0'
    testCompile 'org.springframework:spring-web:4.3.8.RELEASE'
    testCompile 'org.apache.httpcomponents:httpclient:4.5.2'
    testCompile 'org.assertj:assertj-core:3.6.1'
    testCompile 'net.javacrumbs.json-unit:json-unit:1.17.0'
    testCompile 'net.javacrumbs.json-unit:json-unit-fluent:1.17.0'
    testCompile 'org.eclipse.jetty:jetty-server:9.3.11.v20160721'
    testCompile 'org.skyscreamer:jsonassert:1.4.0'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile 'org.powermock:powermock-module-junit4:1.6.6'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
    testCompile 'io.projectreactor.ipc:reactor-netty:0.7.2.RELEASE'
    testCompile 'io.projectreactor:reactor-test:3.1.2.RELEASE'
}

allprojects { subproj ->

    ext.mvnRepoUrlRelease = "$System.env.MAVEN_REPO_URL_RELEASE"
    ext.mvnRepoUrlSnapshot = "$System.env.MAVEN_REPO_URL_SNAPSHOT"
    ext.mvnRepoUser = "$System.env.MAVEN_REPO_USERNAME"
    ext.mvnRepoPass = "$System.env.MAVEN_REPO_PASSWORD"

    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'net.researchgate.release'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    afterReleaseBuild.dependsOn uploadArchives

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "$mvnRepoUrlRelease") {
                    authentication(userName: "$mvnRepoUser", password: "$mvnRepoPass")
                }

                snapshotRepository(url: "$mvnRepoUrlSnapshot") {
                    authentication(userName: "$mvnRepoUser", password: "$mvnRepoPass")
                }

                pom.project {
                    name "${subproj.property('title')}"
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description "${subproj.property('description')}"
                    url 'https://github.com/arvacims/hoverfly-java'

                    scm {
                        connection 'scm:git:git@github.com:arvacims/hoverfly-java.git'
                        developerConnection 'scm:git:git@github.com:arvacims/hoverfly-java.git'
                        url 'git@github.com:arvacims/hoverfly-java.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            name 'Andrew Morgan'
                            email 'andrew.morgan@opencredo.com'
                            organization 'Specto Labs'
                            organizationUrl 'http://www.specto.io'
                        }
                        developer {
                            name 'Tommy Situ'
                            email 'tommy.situ@specto.io'
                            organization 'Specto Labs'
                            organizationUrl 'http://www.specto.io'
                        }
                    }
                }
            }
        }
    }
}

updateVersion.dependsOn subprojects.findResults { it.tasks.findByName('release') }

license {
    header = file('license-header.txt')
    strictCheck true
    include "src/main/**/*.java"
    ext.inceptionYear = 2016
    ext.currentYear = Calendar.getInstance().get(Calendar.YEAR)
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}

// Tasks for updating Hoverfly binaries
import de.undercouch.gradle.tasks.download.Download
import java.util.regex.Pattern

task downloadHoverflyDistributions(type: Download) {
    def downloadUrl = 'https://github.com/SpectoLabs/hoverfly/releases/download'
    def version = project.property('hoverfly-version')
    println "Checking out Hoverfly $version"
    src([
            "$downloadUrl/$version/hoverfly_bundle_linux_386.zip",
            "$downloadUrl/$version/hoverfly_bundle_linux_amd64.zip",
            "$downloadUrl/$version/hoverfly_bundle_OSX_amd64.zip",
            "$downloadUrl/$version/hoverfly_bundle_windows_386.zip",
            "$downloadUrl/$version/hoverfly_bundle_windows_amd64.zip"
    ])
    dest "$buildDir/tmp/dist"
    onlyIfNewer true
}


task updateHoverflyBinaries(dependsOn: downloadHoverflyDistributions) {
    def distDir = file(downloadHoverflyDistributions.dest)
    def extractBinary = { FileTree zip ->
        copy {
            from zip
            into 'src/main/resources/binaries'
            rename { String filename ->
                def p1 = Pattern.compile('hoverfly(.*)')
                def m1 = p1.matcher(filename)
                def extension = m1.find() ? m1.group(1) : ''
                def p2 = Pattern.compile('hoverfly_bundle(.+?)\\.zip')
                def m2 = p2.matcher(zip.asPath)
                def type = m2.find() ? m2.group(1) : ''
                filename = 'hoverfly' + type + extension
            }
        }
    }

    distDir.listFiles()
            .findAll { it.name.endsWith('.zip') }
            .collect { zipTree(it).matching { include 'hoverfly*' } }
            .each { zip -> extractBinary(zip) }
}
